start: expr

?expr: sequence
     | sequence "or" expr     -> or_expr

sequence: element ( "followed by" element )*

element: group
       | repeated_term

group: "group" sequence "end group" repetition?

# repetición opcional antes y/o después del término
repeated_term: repetition? term repetition?

term: base_term "except" base_term   -> t_except
    | base_term
    | range_expr                     -> t_range

# ---------- TERMINOS BASE (CLASES Y LITERALES) ----------

base_term: "letter"             -> t_letter
         | "digit"              -> t_digit
         | "space"              -> t_space
         | "any character"      -> t_any
         | "uppercase letter"   -> t_upper
         | "lowercase letter"   -> t_lower
         | "vowel"              -> t_vowel
         | "consonant"          -> t_consonant
         | "alphanumeric"       -> t_alphanumeric
         | "word character"     -> t_word
         | "hex digit"          -> t_hex
         | "whitespace"         -> t_whitespace
         | "non whitespace"     -> t_non_whitespace
         | CHAR_LITERAL         -> t_char
         | STRING_LITERAL       -> t_string

range_expr: "range" CHAR_LITERAL "to" CHAR_LITERAL

repetition: INT "times"                     -> r_exact
          | "between" INT "and" INT "times" -> r_range
          | "one or more"                   -> r_one_or_more
          | "zero or more"                  -> r_zero_or_more
          | "optional"                      -> r_optional
          | "at least" INT "times"          -> r_at_least
          | "at most" INT "times"           -> r_at_most

CHAR_LITERAL: "'" /[^']/ "'"
STRING_LITERAL: "'" /[^']*/ "'"

%import common.INT
%import common.WS
%ignore WS
